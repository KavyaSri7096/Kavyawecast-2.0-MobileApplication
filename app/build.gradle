apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply from: '../flavors.gradle'
apply from: '../libraries.gradle'

android {

    compileSdkVersion versions.compileSdk

    defaultConfig {

        // App package Id
        applicationId "com.wecast.mobile"

        // Version
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.appVersionCode
        versionName versions.appVersion

        // Renderscript for blur
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        // Testing
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // SDK integration Ids
        manifestPlaceholders = [
                ADMOB_APP_ID: "ca-app-pub-8022422195783695~7199223580",
                HOCKEYAPP_APP_ID: "59e07effc02f41108cb4c7522042f5c8",
                FABRIC_APP_ID: "efa99ecbf88759d98f7a16cc890df5964dabdbb8"
        ]
    }

    /*signingConfigs {
        release {
            applicationVariants.all { variant ->
                def keystorePropertiesFile = rootProject.file("app/src/${variant.productFlavors[0].name}/key/keystore_properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                // Read key store info from file
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }*/

    buildTypes {
        release {
            //signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Mobile-${variant.productFlavors[0].name}-${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':wecast-core')
    implementation project(':wecast-player')

    // Android X support
    api libraries.androidx.base

    // Dependency Injection
    implementation libraries.dagger.base
    annotationProcessor libraries.dagger.compiler
    implementation libraries.dagger.android
    annotationProcessor libraries.dagger.androidProcessor
    implementation libraries.dagger.androidSupport

    // Glide
    implementation libraries.glide.base
    annotationProcessor 'androidx.annotation:annotation:1.0.1'
    annotationProcessor libraries.glide.compiler

    // LeakCanary
    debugImplementation libraries.leakcanary.debug
    releaseImplementation libraries.leakcanary.release

    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }
}
